plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.15.0'
}

group = 'br.com.starwars'
version = '1.0.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'org.apache.commons:commons-lang3:3.17.0'

    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.h2database:h2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: [
                            'br/com/starwars/domain/Planet*',
                            'br/com/starwars/StarwarsPlanetApiApplication*'
                    ])
                })
        )
    }
}

pitest {
    junit5PluginVersion = '1.2.1'
    pitestVersion = '1.15.2'

    targetClasses = ['br.com.starwars.*'] // Pacotes a serem testados
    targetTests = ['br.com.starwars.*Test'] // Testes a serem executados
    excludedClasses = [
            'br.com.starwars.domain.Planet', 'br.com.starwars.web.handler.GeneralExceptionHandler'
    ] // Excluir classes de um pacote específico
    threads = 4 // Número de threads para execução
    outputFormats = ['HTML'] // Formatos de saída
    timestampedReports = false // Desabilita timestamps nos relatórios
    mutationThreshold = 80 // Percentual mínimo de mutações que devem ser cobertas
    coverageThreshold = 80 // Percentual mínimo de cobertura de código
    mutators = ['DEFAULTS'] // Tipos de mutações a serem aplicadas
    verbose = true // Habilita logs detalhados
}
